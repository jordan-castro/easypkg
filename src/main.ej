import "std"
import "src/add.ej"

import "going_up"

// Load project config
@const(fs, require('node:fs'))

javascript {
    const {exec} = require('child_process');
    const readline = require('node:readline');
}

args = process.argv.slice(2, process.argv.length)

macro kill(code) {
    process.exit(#code)
}

@const(help, "
ezpkg - EasyJS Package Manager

Usage:
    ezpkg <command> [options]

Commands:
    init             Create a new EasyJS project interactively.
    run              Run the main source file defined in the project config.
    compile          Compile the project source file into JavaScript.
        minify       (Optional) Minify the compiled output.

Options:
    --help           Show this help message.
    --version        Show the current version of ezpkg.

Example:
    ezpkg init
    ezpkg compile minify
    ezpkg run
")

async fn init() {
    
    @const(rl, readline.createInterface({
        input: process.stdin,
        output: process.stdout
    }))

    @print("Welcome to the EasyJS project manager")

    fn get_input(msg, fallback) {
        return new Promise(fn(resolve) {
            rl.question(msg, fn(answer) {
                if answer == "" or answer == null {
                    resolve(fallback)
                } else {
                    resolve(answer)
                }
            })
        })
    }

    name = await get_input("project name: ", "project")
    author_name = await get_input("author name: ", "")
    author_email = await get_input("author email: ", "")
    runtime = await get_input("runtime: ", "node")

    if !fs.existsSync(name) {
        fs.mkdirSync(name)
    }
    if !fs.existsSync('$name/src') {
        fs.mkdirSync('$name/src')
    }

    fs.writeFileSync('$name/src/main.ej', "import 'std'\n@print('Hello from easyJS')\n")
    fs.writeFileSync('$name/pkg.ejconfig', JSON.stringify({
        name,
        description: "A new easyjs package.",
        author: {
            name: author_name,
            email: author_email
        },
        source: 'src/main.ej',
        output: '$name.js',
        runtime,
        global: false
    }, undefined, 4))
    rl.close()
}

fn get_config() {
    // Get the current config of the cwd and run the source
    files = fs.readdirSync('./')
    config = null
    for file in files {
        if file.endsWith('.ejconfig') {
            config = JSON.parse(fs.readFileSync(file))
            break
        }
    }

    return config    
}

fn call_easyjs(command) {
    exec(command, fn(error, stdout, stderr) {
        if error {
            @print('error executing program: ${error.message}')
            process.exit(0)
        }
        if stderr != "" {
            @print(stderr)
        }
        if stdout != "" {
            @print(stdout)
        }
        @kill(0)
    })
}

if args.length == 0 {
    @print(help)
    @kill(0)
}

if args[0] == "--help" {
    @print(help)
    @kill(0)
}

if args[0] == "--version" {
    @print("0.0.1")
    @kill(0)
}

if args[0] == "init" {
    // let's do some init
    async {
        await init()
        @kill(0)
    }
} elif args[0] == "test" {
    @print("This worked!")
} elif args[0] == "compile" {
    config = get_config()
    if config == null {
        @print("No config found")
        @kill(1)
    }

    minify = ""
    if args[1] == "minify" {
        minify = "--minify"
    }

    call_easyjs('easyjs compile ${config.source} --output ${config.output} $minify')
} elif args[0] == "run" {
    config = get_config()
    if config == null {
        @print("No config found")
        @kill(1)
    }

    call_easyjs('easyjs run ${config.source} --runtime ${config.runtime}')
} elif args[0] == "add" {
    // We have to add a easyjs package! Que locura loco
    // get current directory
    cdir = get_cwd()

    @print(cdir)
    
    call_easyjs('easyjs install ${args[1]} --forced-dir $cdir/packages/')
}